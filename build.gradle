buildscript {
    ext.kotlin_version = '1.3.50'
    ext.h2_version = '1.4.193'
    ext.jooq_version = '3.9.0'
    ext.flyway_version = '4.0.3'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("org.jooq:jooq-codegen:$jooq_version")
        classpath("org.flywaydb:flyway-core:$flyway_version")
        classpath("com.h2database:h2:$h2_version")
    }
}

subprojects {
    group 'de.mkammerer.restwars'
    version '0.3.0-SNAPSHOT'

    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    }

    repositories {
        mavenCentral()
    }

    publishing {
        repositories {
            maven {
                credentials {
                    username System.getenv('MAVEN_USER')
                    password System.getenv('MAVEN_PASSWORD')
                }
                url 'https://api.bintray.com/maven/phxql/maven/RESTwars/;publish=1'
            }
        }

        publications {
            bintray(MavenPublication) {
                from components.java

                afterEvaluate {
                    artifact sourcesJar
                }
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

wrapper {
    gradleVersion = '5.6.3'
}
