group 'de.mkammerer'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.0.0'
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

// Git helper functions
apply from: 'gradle/git.gradle'

mainClassName = 'restwars.rest.RestWarsKt'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // REST layer
    compile('com.sparkjava:spark-core:2.3') {
        exclude module: 'slf4j-simple'
    }
    // Logging API
    compile 'org.slf4j:slf4j-api:1.7.14'
    // JSON
    compile 'com.google.code.gson:gson:2.5'
    // Validation
    compile 'org.hibernate:hibernate-validator:5.2.3.Final'
    // YAML parsing
    compile 'org.yaml:snakeyaml:1.16'

    // Needed for validation
    runtime 'javax.el:javax.el-api:2.2.5'
    runtime 'org.glassfish.web:javax.el:2.2.6'

    // Logging backend
    runtime 'ch.qos.logback:logback-classic:1.1.3'

    // Testing
    testCompile('junit:junit:4.12')
}
repositories {
    mavenCentral()
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [
            "project.version": project.version,
            "git.hash"       : getCheckedOutGitCommitHash()
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
