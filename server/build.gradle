apply plugin: 'application'

// Git helper functions
apply from: "$rootProject.projectDir/gradle/git.gradle"

mainClassName = 'restwars.RestWarsKt'

dependencies {
    compile project(':api')

    // REST layer
    compile('com.sparkjava:spark-core:2.5.4') {
        exclude module: 'slf4j-simple'
    }
    // Validation
    compile 'org.hibernate:hibernate-validator:5.2.3.Final'
    // Logging API
    compile 'org.slf4j:slf4j-api:1.7.14'
    // JSON & YAML
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.5'

    // Metrics
    compile 'io.dropwizard.metrics:metrics-core:3.1.2'
    compile 'io.dropwizard.metrics:metrics-json:3.1.2'

    // Needed for validation
    runtime 'javax.el:javax.el-api:2.2.5'
    runtime 'org.glassfish.web:javax.el:2.2.6'

    // Logging backend
    runtime 'ch.qos.logback:logback-classic:1.1.8'

    // Database
    compile "org.flywaydb:flyway-core:$flyway_version"
    compile "com.h2database:h2:$h2_version"
    compile 'com.mchange:c3p0:0.9.5.2'

    // needed by old jooq version, removed in java 9
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    compile "org.jooq:jooq:$jooq_version"

    // Testing
    testCompile 'junit:junit:4.12'
}


import groovy.xml.MarkupBuilder
import org.apache.tools.ant.filters.ReplaceTokens
import org.flywaydb.core.Flyway
import org.jooq.util.GenerationTool

import javax.xml.bind.JAXB

processResources {
    filter ReplaceTokens, tokens: [
            "project.version": project.version,
            "git.hash"       : getCheckedOutGitCommitHash()
    ]
}

task('generateJooq').doLast {
    // Create in-memory database with schema
    def flyway = new Flyway()
    flyway.setDataSource('jdbc:h2:mem:jooq;DB_CLOSE_DELAY=-1', '', '')
    def file = new File("server/src/main/resources/db/migration")
    flyway.setLocations("filesystem:$file.absolutePath")
    flyway.migrate()

    // Generate jooq classes
    def writer = new StringWriter()
    new MarkupBuilder(writer)
            .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.9.0.xsd') {
        jdbc() {
            driver('org.h2.Driver')
            url('jdbc:h2:mem:jooq;DB_CLOSE_DELAY=-1')
            user('')
            password('')
        }
        generator() {
            database() {
                inputSchema('PUBLIC')
                excludes('SCHEMA_VERSION')
            }
            generate([:]) {
                pojos false
                daos false
            }
            target() {
                packageName('restwars.storage.jooq')
                directory('server/src/main/java')
            }
        }
    }

    GenerationTool.generate(JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.util.jaxb.Configuration.class))
}